/*
 * Copyright (c) 2024-2025 Linus Andera
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import de.linusdev.ljgel.build.CPPBuildType
import de.linusdev.lutils.version.ReleaseType
import de.linusdev.lutils.version.Version


plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'de.linusdev.lutils.gradle' version '+'
    id 'ljgel'
}

Version engineVersion = Version.of(ReleaseType.valueOf(engine_release_type), engine_version)
CPPBuildType nativeBuildType = engineVersion.type() == ReleaseType.DEVELOPMENT_BUILD ? CPPBuildType.MSVC_DEBUG : CPPBuildType.MSVC_RELEASE

group engine_group
version engineVersion.version().asUserFriendlyString;

targetCompatibility = JavaVersion.VERSION_17
sourceCompatibility = JavaVersion.VERSION_17
compileJava.options.encoding = 'UTF-8'

downloads {

    urls = List.of(
            'https://raw.githubusercontent.com/KhronosGroup/Vulkan-Docs/main/xml/vk.xml',
            "https://raw.githubusercontent.com/KhronosGroup/Vulkan-Docs/main/xml/video.xml"
    )

    outputFileNames = List.of(
            'vk.xml',
            'video.xml'
    )

    backup = true
}

vulkanWrapper {
    basePackage = "$engine_group.${engine_package}.nat"
    vulkanXmlFileDir = downloads
}

compileNativeLibraries {
    nativeLibName = "$engine_package"
    resourcePackage = "$engine_group.${engine_package}.libs"
    buildType = nativeBuildType
}

compileTestShaders {
    shaderLocation = 'src/test/glsl/test/'
    compiledShadersRootResourcesPackage = "$engine_group.${engine_package}.vulkan.shaders"
}

//processTestResources.dependsOn(glslCTestTask)
//processResources.dependsOn(glslCTestTask)

constantGen {
    basePackage = "$engine_group.$engine_package"
    add("ENGINE_VERSION", engineVersion)
    add("NATIVE_LIB_NAME", "${engine_package}.${nativeBuildType.libFileEnding}")
}

sourceSets {
    main {
        java {
            srcDir(vulkanWrapper)
        }
        resources {
            srcDir(compileNativeLibraries)
        }
    }
    test {
        resources {
            srcDir(compileTestShaders)
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

jar {

}


javadoc {
    options.encoding = 'UTF-8'
    options.addStringOption('Xdoclint:none', '-quiet')
    title = engine_short_name

    configure(options) {
        tags(
                'implNote:a:Implementation Note:',
                'implSpec:a:Implemenatation Requirements:',
                'apiNote:a:API Note:',
                'ljgel.addedByExtension:a:Added By Extension:',
                'ljgel.vk.optional:a:Optional',
                'ljgel.vk.noAutoValidity:a:NoAutoValidity',
                'ljgel.vk.limitType:a:LimitType',
                'ljgel.vk.len:a:Len',
                'ljgel.cDef:a:C-Definition'
        )
    }
}

test {
    jvmArgs '-XX:+CreateCoredumpOnCrash';
}

repositories {
    mavenLocal()
    mavenCentral()
}

tasks.register("testWithRenderDoc", Exec.class) {
    def javaHome = System.properties['java.home']

    commandLine = [
            "C:\\Program Files\\RenderDoc\\renderdoccmd.exe",
            "capture",
            "--opt-hook-children",
            "--wait-for-exit",
            "--working-dir",
            ".",
            "$javaHome/bin/java.exe",
            "-Xmx64m",
            "-Xms64m",
            "-Dorg.gradle.appname=gradlew",
            "-Dorg.gradle.java.home=$javaHome",
            "-classpath",
            "gradle/wrapper/gradle-wrapper.jar",
            "org.gradle.wrapper.GradleWrapperMain",
            ":test",
            "--tests",
            "\"$engine_group.${engine_package}.engine.vk.VulkanEngineTest.test\""
    ]

}

dependencies {
    api 'de.linusdev:lutils:2.0.1'
    implementation 'de.linusdev:llog:1.0.2'
    api 'org.jetbrains:annotations:24.0.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
    workingDir = "/wd/"
}

publishing {
    publications {

        ljgel(MavenPublication) {
            artifactId = engine_package
            from components.java
            pom {
                name = engine_short_name
                description = engine_short_description
                url = 'https://www.linusdev.de'
                groupId = 'de.linusdev'
                version = project.version

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'linusdev'
                        name = 'Linus Andera'
                        email = 'linus@linusdev.de'
                        organizationUrl = 'https://www.linusdev.de'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/lni-dev/ljgel.git'
                    developerConnection ='scm:git:ssh://github.com:lni-dev/ljgel.git'
                    url = 'https://github.com/lni-dev/ljgel/tree/master'
                }
            }
        }
    }
}